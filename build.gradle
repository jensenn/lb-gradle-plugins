
// *****************************************************************************
//
// *****************************************************************************

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven'

    version      = '1.0.0.SNAPSHOT'
    group        = 'com.github.lburgazzoli'

    ext {
        junitVersion     = '4.11'
        prefix           = 'gradle-plugin'
        isReleaseVersion = !version.endsWith("SNAPSHOT")
        gitRoot          = "https://github.com/lburgazzoli"
        gitProject       = "https://github.com/lburgazzoli/lb-gradle-plugins"
        deploySnapshot   = "https://oss.sonatype.org/content/repositories/snapshots"

        if(project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
            ciUsr = sonatypeUsername
            ciPwd = sonatypePassword
        } else {
            ciUsr = "$System.env.CI_DEPLOY_USERNAME"
            ciPwd = "$System.env.CI_DEPLOY_PASSWORD"
        }
    }

    repositories {
        mavenCentral()    
        mavenLocal()
    }

    dependencies {
        compile gradleApi()
        compile localGroovy()

        testCompile "junit:junit:$junitVersion"
		testCompile ('org.spockframework:spock-core:0.7-groovy-1.8') {
			exclude module: "junit-dep"
			exclude module: "groovy-all"
		}
    }

    jar {
        baseName = "${project.prefix}-${project.name}"

        manifest {
            attributes['Implementation-Title'   ] = "${group}.${project.prefix}-${project.name}-${project.version}"
            attributes['Implementation-Version' ] = project.version
            attributes['Implementation-Vendor'  ] = 'lb'
        }
    }

    // *************************************************************************
    //
    // *************************************************************************

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

}

// *****************************************************************************
//
// *****************************************************************************

task wrapper(type: Wrapper) {
    gradleVersion = '1.9'
}

